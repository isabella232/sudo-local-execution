plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.3.2'
}

defaultTasks 'clean','build'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
sourceCompatibility = 1.8
ext.rundeckPluginVersion= '2.0'
ext.rundeckVersion= '3.0.x'


repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    pluginLibs

    compile {
        extendsFrom pluginLibs
    }
}

scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = ''
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}
project.version = scmVersion.version


dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    compile 'org.rundeck:rundeck-core:3.0.1+'

    testCompile 'junit:junit:4.12'
    testCompile "org.codehaus.groovy:groovy-all:2.4.15"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "cglib:cglib-nodep:2.2.2"
    testCompile 'org.objenesis:objenesis:1.4'}

ext.pluginClassNames='com.rundeck.plugin.LocalCommandStep,com.rundeck.plugin.LocalInlineScriptStep,com.rundeck.plugin.LocalNodeExecutor,com.rundeck.plugin.LocalFileCopier'


task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"

    manifest {
        def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': version
        attributes 'Rundeck-Plugin-Name': 'LocalCommandStep'
        attributes 'Rundeck-Plugin-Description': 'Provide a short description of your plugin here.'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.x'
        attributes 'Rundeck-Plugin-Tags': 'java,notification'
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion, 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"

    }

    dependsOn(copyToLib)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}
